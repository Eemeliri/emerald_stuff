const LOCALID_ZAPDOS = 1

mapscripts NewMauville_Inside3F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_CAUGHT_ZAPDOS, tryHideZapdos)
        call_if_unset(FLAG_CAUGHT_ZAPDOS, tryShowZapdos)
    }
    MAP_SCRIPT_ON_RESUME {
        call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, NewMauville_Inside3F_OnResume)
    }
}



script tryHideZapdos {
    setflag(FLAG_HIDE_ZAPDOS)
    return
}

script tryShowZapdos {
    goto_if_set(FLAG_DEFEATED_ZAPDOS, Common_EventScript_NopReturn)
    clearflag(FLAG_HIDE_ZAPDOS)
    return
}

script NewMauville_Inside3F_OnResume {
    call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, tryRemoveZapdos)
}

script tryRemoveZapdos {
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
       goto(Common_EventScript_NopReturn)
    }
    removeobject(LOCALID_ZAPDOS)
    return
}

script EventScript_Zapdos {
    lock
    faceplayer
    waitse
    delay(20)
    playmoncry(SPECIES_ZAPDOS, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(20)
    seteventmon(SPECIES_ZAPDOS, 40)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        goto(defeatedZapdos)
    } elif (var(VAR_RESULT) == B_OUTCOME_RAN) {
        goto(ranZapdos)
    } elif (var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED) {
        goto(ranZapdos)
    }
    setflag(FLAG_CAUGHT_ZAPDOS)
    release
    end
}

script ranZapdos {
    setvar(VAR_0x8004, SPECIES_ZAPDOS)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

script defeatedZapdos {
    setvar(VAR_0x8004, SPECIES_ZAPDOS)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}