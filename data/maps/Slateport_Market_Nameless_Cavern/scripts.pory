const LOCALID_AZELF = 1

mapscripts Slateport_Market_Nameless_Cavern_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_CAUGHT_AZELF, tryHideAzelf)
        call_if_unset(FLAG_CAUGHT_AZELF, tryShowAzelf)
    }
    MAP_SCRIPT_ON_RESUME {
        call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, NamelessCavern_OnResume)
    }
}

script tryHideAzelf {
    setvar(VAR_TEMP_1, 1)
    setvar(VAR_TEMP_2, 1)
    setflag(FLAG_HIDE_AZELF)
    return
}

script tryShowAzelf {
    setvar(VAR_TEMP_1, 1)
    goto_if_set(FLAG_DEFEATED_AZELF, Common_EventScript_NopReturn)
    setvar(VAR_TEMP_1, 0)
    return
}

script tryRemoveAzelf {
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
       goto(Common_EventScript_NopReturn)
    }
    removeobject(LOCALID_AZELF)
    return
}

script NamelessCavern_OnResume {
    if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
        goto(tryRemoveAzelf)
    }
}


script enterNamelessCavern{
    lockall
    setvar(VAR_TEMP_2, 1)
    msgbox(format ("Clear air surrounds the area. It's lifting your spirits."))
    release
    end
}

script spawnAzelf {
    lockall
    setvar(VAR_TEMP_1, 1)
    playbgm(MUS_NONE, FALSE)

    playmoncry(SPECIES_AZELF, CRY_MODE_NORMAL)
    waitmoncry

    addobject(LOCALID_AZELF)

    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, player_movement)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitse
    applymovement(1, azelf_movement)
    waitmovement(0)

    playmoncry(SPECIES_AZELF, CRY_MODE_ENCOUNTER)
    msgbox(format("Azeeeeeeee!!"))
    delay(40)
    waitmoncry

    seteventmon(SPECIES_AZELF, 40)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(BattleSetup_StartLegendaryBattle)
    waitstate

    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    setvar(VAR_LAST_TALKED, LOCALID_AZELF)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        goto(defeatedAzelf)
    } elif (var(VAR_RESULT) == B_OUTCOME_RAN) {
        goto(ranAzelf)
    } elif (var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED) {
        goto(ranAzelf)
    }
    setflag(FLAG_CAUGHT_AZELF)
    returnqueststate(QUEST_2)
    goto_if_eq(VAR_RESULT, 1, startLegendaryQuestAzelf)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_3)
    releaseall
    end
}

script startLegendaryQuestAzelf {
    startquest(QUEST_2)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_3)
	release
	end
}

script ranAzelf {
    setvar(VAR_0x8004, SPECIES_AZELF)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

script defeatedAzelf {
    setvar(VAR_0x8004, SPECIES_AZELF)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

movement player_movement {
    emote_exclamation_mark
    face_up
    step_end
}

movement azelf_movement {
    walk_slow_down * 5
    step_end
}