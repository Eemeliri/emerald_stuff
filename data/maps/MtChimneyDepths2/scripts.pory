const LOCALID_MOLTRES = 1

mapscripts MtChimneyDepths2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_CAUGHT_MOLTRES, tryHideMoltres)
        call_if_unset(FLAG_CAUGHT_MOLTRES, tryShowMoltres)
    }
    MAP_SCRIPT_ON_RESUME {
        call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, MtChimneyDepths2_OnResume)
    }
}


script tryHideMoltres {
    setflag(FLAG_HIDE_MOLTRES)
    return
}

script tryShowMoltres {
    goto_if_set(FLAG_DEFEATED_MOLTRES, Common_EventScript_NopReturn)
    clearflag(FLAG_HIDE_MOLTRES)
    return
}

script MtChimneyDepths2_OnResume {
    call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, tryRemoveMoltres)
}

script tryRemoveMoltres {
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
       goto(Common_EventScript_NopReturn)
    }
    removeobject(LOCALID_MOLTRES)
    return
}

script EventScript_Moltres {
    lock
    faceplayer
    waitse
    delay(20)
    playmoncry(SPECIES_MOLTRES, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(20)
    seteventmon(SPECIES_MOLTRES, 40)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        goto(defeatedMoltres)
    } elif (var(VAR_RESULT) == B_OUTCOME_RAN) {
        goto(ranMoltres)
    } elif (var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED) {
        goto(ranMoltres)
    }
    setflag(FLAG_CAUGHT_MOLTRES)
    release
    end
}

script ranMoltres {
    setvar(VAR_0x8004, SPECIES_MOLTRES)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

script defeatedMoltres {
    setvar(VAR_0x8004, SPECIES_MOLTRES)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}