const LOCALID_SHAYMIN = 1

mapscripts FlowerParadise_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_CAUGHT_SHAYMIN, tryHideShaymin)
        call_if_unset(FLAG_CAUGHT_SHAYMIN, tryShowShaymin)
    }
    MAP_SCRIPT_ON_RESUME {
        call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, FlowerParadise_OnResume)
    }
}

script tryHideShaymin {
    setflag(FLAG_HIDE_SHAYMIN)
    return
}

script tryShowShaymin {
    goto_if_set(FLAG_DEFEATED_SHAYMIN, Common_EventScript_NopReturn)
    clearflag(FLAG_HIDE_SHAYMIN)
    return
}

script FlowerParadise_OnResume {
    call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, tryRemoveShaymin)
}

script tryRemoveShaymin {
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
       goto(Common_EventScript_NopReturn)
    }
    removeobject(LOCALID_SHAYMIN)
    return
}

script EventScript_Shaymin {
    lock
    faceplayer
    playmoncry(SPECIES_SHAYMIN, CRY_MODE_NORMAL)
    msgbox(format("Shay.. Shaymin?"))
    waitse
    delay(20)
    playmoncry(SPECIES_SHAYMIN, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(20)
    playmoncry(SPECIES_SHAYMIN_SKY, CRY_MODE_ENCOUNTER)
    msgbox(format("Shayyy!!"))
    settotemboost(1, 2, 2, 2, 2, 2, 2, 0)
    seteventmon(SPECIES_SHAYMIN_SKY, 75)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
        goto(ranShaymin)
    }
    setflag(FLAG_CAUGHT_SHAYMIN)
    addvar(VAR_SHAYMIN_QUEST_STATE, 1)
    returnqueststate(QUEST_2)
    goto_if_eq(VAR_RESULT, 1, EventScript_StartLegendaryQuestShaymin)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_33)
    release
    end
}

script EventScript_StartLegendaryQuestShaymin {
	startquest(QUEST_2)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_33)
	release
	end
}

script ranShaymin {
    setvar(VAR_0x8004, SPECIES_SHAYMIN)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

script teleportOutFlowerParadise {
    lock
    msgbox(format("Touch the stone to return where you came from?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        warpspinenter(MAP_EVER_GRANDE_CITY, 28, 8)
        release
        end
    }
    release
    end
}