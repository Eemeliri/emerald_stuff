const LOCALID_MEWTWO = 1
const LOCALID_DITTO = 3

mapscripts NamelessDungeonB2F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_CAUGHT_MEWTWO, tryHideMewtwo)
        call_if_unset(FLAG_CAUGHT_MEWTWO, tryShowMewtwo)
    }
    MAP_SCRIPT_ON_RESUME {
        call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, NamelessDungeonB2F_OnResume)
    }
}



script tryHideMewtwo {
    setflag(FLAG_HIDE_MEWTWO)
    return
}

script tryShowMewtwo {
    goto_if_set(FLAG_DEFEATED_MEWTWO, Common_EventScript_NopReturn)
    clearflag(FLAG_HIDE_MEWTWO)
    return
}

script NamelessDungeonB2F_OnResume {
    call_if_set(FLAG_SYS_CTRL_OBJ_DELETE, tryRemoveMewtwo)
}

script tryRemoveMewtwo {
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) != B_OUTCOME_CAUGHT) {
       goto(Common_EventScript_NopReturn)
    }
    removeobject(LOCALID_MEWTWO)
    return
}

script EventScript_Mewtwo {
    lock
    faceplayer
    waitse
    delay(20)
    playmoncry(SPECIES_MEWTWO, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(20)
    seteventmon(SPECIES_MEWTWO, 80)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        goto(defeatedMewtwo)
    } elif (var(VAR_RESULT) == B_OUTCOME_RAN) {
        goto(ranMewtwo)
    } elif (var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED) {
        goto(ranMewtwo)
    }
    setflag(FLAG_CAUGHT_MEWTWO)
    returnqueststate(QUEST_2)
    goto_if_eq(VAR_RESULT, 1, startLegendaryQuestMewtwo)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_4)
    release
    end
}

script startLegendaryQuestMewtwo {
    startquest(QUEST_2)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_2, SUB_QUEST_4)
	release
	end
}

script GreenMewTransform {
    lockall
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, panToGreen)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    delay(60)
    msgbox(format("???: Alright we made it to the end, good job Ditto!"))
    waitmessage
    
    delay(30)
    playmoncry(SPECIES_MEW, CRY_MODE_NORMAL)
    msgbox("Dittodii!", MSGBOX_AUTOCLOSE)
    
    applymovement(3, mewJump)
    waitmoncry
    delay(30)
    playse(SE_BALL_OPEN)
    removeobject(LOCALID_DITTO)
    addobject(4)
    setobjectxy(4, 52, 23)
    delay(60)
    playmoncry(SPECIES_DITTO, CRY_MODE_NORMAL)
    waitmoncry
    delay(60)
    playse(SE_BALL_OPEN)
    removeobject(4)
    addobject(5)
    setobjectxy(5, 52, 23)
    delay(30)
    removeobject(5)
    setflag(FLAG_HIDE_DITTO)
    playse(SE_PIN)
    applymovement(2, greenNoticePlayer)
    waitmovement(2)
    applymovement(OBJ_EVENT_ID_CAMERA, panBackFromGreen)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    delay(60)
    special(RemoveCameraObject)
    setvar(VAR_NAMELESS_DUNGEON_GREEN_STATE, 1)
    releaseall
    end
}

script challengeGreenFromLeft {
    lock
    faceplayer
    msgbox(format("???: Since you've made it this far, I assume you must be the champion of Hoenn."))
    msgbox(format("???: It's so nice to finally meet you in person! My name is Green!"))
    msgbox(format("Green: I'm currently travelling across regions to fill up the Pokédex for Professor Oak and to train my partner Pokémon."))
    waitmessage
    applymovement(2, faceGreenUp)
    waitmovement(2)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, panCameraToMewtwo)
    msgbox(format("Green: See that strong Pokémon up there? That's Mewtwo, an artificial species created solely for battles. It has been resting in this cave for a very long time."))
    delay(60)
    applymovement(OBJ_EVENT_ID_CAMERA, panCameraBackFromMewtwo)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    delay(20)
    applymovement(2, GreenFacePlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, facePlayerRightGreen)
    special(RemoveCameraObject)
    msgbox(format("Green: How about we fight over who gets to catch it? I'll even throw you a little bonus if you manage to beat me!"))
    waitmessage
    msgbox(format("Green: Ready? Don't except an easy fight!"))
    trainerbattle_no_intro(TRAINER_GREEN_MEWTWO, format("Incredible! You are just as good as I had hoped."))
    msgbox(format("Green: Even though I lost I don't feel sad or angry, in fact I feel happy to have battled you!"))
    msgbox(format("Green: As promised, you will get to catch Mewtwo, although if you fail I will come to pick up your pieces."))
    msgbox(format("Green: And that added bonus I was speaking of? Here you go!"))
    waitmessage
    giveitem(ITEM_MEWTWONITE_X)
    waitmessage
    giveitem(ITEM_MEWTWONITE_Y)
    waitmessage
    msgbox(format("Green: I'm going to head off into another region now, perhaps Alola? I might swing by the Pokémon World Tournament while I'm still here, hope to see you in there for a rematch."))
    waitmessage
    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_HIDE_GREEN)
    removeobject(2)
    delay(60)
    fadescreen(FADE_FROM_BLACK)
    delay(20)
    release
    end
}

movement panCameraToMewtwo {
    walk_up * 4
    step_end
}

movement panCameraBackFromMewtwo {
    walk_down * 4
    step_end
}

movement panToGreen{
    walk_right * 4
    step_end
}

movement mewJump {
    jump_in_place_left
    step_end
}

movement greenNoticePlayer {
    emote_exclamation_mark
    delay_16
    delay_16
    face_left
    step_end
}

movement panBackFromGreen {
    walk_left * 4
    step_end
}

movement faceGreenUp {
    face_up
    step_end
}

movement facePlayerUpGreen {
    face_up
    step_end
}

movement facePlayerRightGreen {
    face_right
    step_end
}

movement GreenFacePlayer {
    face_player
    step_end
}

script ranMewtwo {
    setvar(VAR_0x8004, SPECIES_MEWTWO)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}

script defeatedMewtwo {
    setvar(VAR_0x8004, SPECIES_MEWTWO)
    goto(Common_EventScript_LegendaryFlewAway)
    end
}